plugins {
    id 'java'
    id 'application'
}

description 'ICEpdf viewer reference implementation project'

mainClassName = "org.icepdf.ri.viewer.Launcher"
applicationDefaultJvmArgs = ["-Xms64m", "-Xmx1024m"]

def sectionName = 'org/icepdf/ri/'
def baseJarName = 'icepdf'
def baseAppendixName  = 'viewer'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':core:core-awt')
    implementation project(':core:core-fonts')
    // signature validation.
    implementation 'org.bouncycastle:bcprov-jdk18on:' + "${BOUNCY_VERSION}"
    implementation 'org.bouncycastle:bcpkix-jdk18on:' + "${BOUNCY_VERSION}"
    // tests
    testImplementation(platform("org.junit:junit-bom:${JUNIT_BOM_VERSION}"))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

// generatePomFileForViewerJarPublication
publishing {
    publications {
        viewerJar(MavenPublication) {
            from components.java
            afterEvaluate {
                groupId 'org.icepdf.os'
                artifactId 'icepdf-viewer'
                version "${VERSION + (RELEASE_TYPE?.trim() ? '-' + RELEASE_TYPE : '')}"
                pom.withXml {
                    asNode().appendNode('description', 'ICEpdf core rendering library.')
                    asNode().appendNode('url', 'https://github.com/pcorless/icepdf')
                    asNode().appendNode('scm')
                            .appendNode('connection', 'scm:git:https://github.com/pcorless/icepdf').parent()
                            .appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.txt').parent()
                            .appendNode('tag', 'icepdf-' + version + '-maven')
                    asNode().appendNode('licenses').appendNode('license')
                            .appendNode('name', 'Apache License, Version 2.0').parent()
                            .appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.html').parent()
                            .appendNode('distribution', 'repo')
                }
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jar {
    archiveBaseName.set('icepdf')
    archiveAppendix.set("viewer")
    archiveVersion.set("${VERSION}")
    archiveClassifier.set("${RELEASE_TYPE}")

    doFirst {
        manifest {
            attributes ('Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')
            // executable jar
            attributes("Main-Class": 'org.icepdf.ri.viewer.Launcher')
            if (!configurations.runtimeClasspath.isEmpty()) {
                attributes('Class-Path':
                        configurations.runtimeClasspath.files.collect{it.name}.join(' '))
            }
        }
    }

    manifest {
        // section names attributes
        attributes("Implementation-Title": "${archiveBaseName.get() + '-' + archiveAppendix.get()}", "${sectionName}")
        attributes("Implementation-Version": "${VERSION + (RELEASE_TYPE?.trim()? '-' + RELEASE_TYPE:'')}", "${sectionName}")
        attributes("Implementation-Vendor": "${COMPANY}", "${sectionName}")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Assembles a jar archive containing the main classes source code.'
    group = 'Documentation'
    archiveBaseName.set("${baseJarName}")
    archiveAppendix.set("${baseAppendixName}")
    archiveVersion.set("${VERSION}")
    archiveClassifier.set("sources")
    manifest {
        attributes("Implementation-Title": "${archiveBaseName.get() + '-' + archiveAppendix.get()}", "${sectionName}")
        attributes("Implementation-Version": "${VERSION + (RELEASE_TYPE?.trim()? '-' + RELEASE_TYPE:'')}", "${sectionName}")
        attributes("Implementation-Vendor": "${COMPANY}", "${sectionName}")
    }
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc
    archiveClassifier.set("javadoc")
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
