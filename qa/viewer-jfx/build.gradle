plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

repositories {
    mavenCentral()
}

description 'ICEpdf qa framework application'

mainClassName = "org.icepdf.qa.viewer.Launcher"
applicationDefaultJvmArgs = ["-Xms64m", "-Xmx4096m"]

def sectionName = 'org/icepdf/qa/'

repositories {
    mavenCentral()
    jcenter()
}

jar {
    archiveBaseName.set('icepdf-qa')
    archiveAppendix.set("viewer")
    archiveVersion.set("${VERSION}")
    archiveClassifier.set("${RELEASE_TYPE}")

    doFirst {
        manifest {
            attributes ('Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')
            // executable jar
            attributes("Main-Class": 'org.icepdf.qa.viewer.Launcher')
            if (!configurations.runtimeClasspath.isEmpty()) {
                attributes('Class-Path':
                        configurations.runtimeClasspath.files.collect{it.name}.join(' '))
            }
        }
    }

    manifest {
        // section names attributes
        attributes("Implementation-Title": "${archiveBaseName.get() + '-' + archiveAppendix.get()}", "${sectionName}")
        attributes("Implementation-Version": "${VERSION + (RELEASE_TYPE?.trim()? '-' + RELEASE_TYPE:'')}", "${sectionName}")
        attributes("Implementation-Vendor": "${COMPANY}", "${sectionName}")
    }
}

javafx {
    version = "11.0.2"
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.graphics' ]
}


dependencies {
    // jackson json.
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.6'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.6'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.6'
    implementation group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    implementation group: 'junit', name: 'junit', version: '4.12'
    // signature validation.
    implementation 'org.bouncycastle:bcprov-jdk15on:' + "${BOUNCY_VERSION}"
    implementation 'org.bouncycastle:bcprov-ext-jdk15on:' + "${BOUNCY_VERSION}"
    implementation 'org.bouncycastle:bcpkix-jdk15on:' + "${BOUNCY_VERSION}"
    // tiff, jpeg2000 and jbig decoding
    implementation 'com.twelvemonkeys.imageio:imageio-tiff:'  + "${MONKEY_VERSION}"
    implementation 'com.github.jai-imageio:jai-imageio-jpeg2000:' + "${JAI_VERSION}"
    implementation 'org.apache.pdfbox:jbig2-imageio:' + "${JBIG2_VERSION}"

}
