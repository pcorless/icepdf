apply plugin: 'distribution'
apply plugin: 'project-report'

allprojects {
    apply plugin: 'java'

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    // show api deprecation
//    gradle.projectsEvaluated {
//        tasks.withType(JavaCompile) {
//            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
//        }
//    }
}

subprojects{
    apply plugin: 'maven-publish'
}

evaluationDependsOn(':core:core-awt')

defaultTasks 'taskReport', 'dependencyReport', 'sourcesJar', 'genPomFileForCoreJarPub', 'genPomFileForViewerJarPub','osDistZip', 'osDistTar'

// Main distribution for os bundle
distributions {
    os {
        distributionBaseName.set("icepdf-os")
        version "${VERSION + (RELEASE_TYPE?.trim()? '-' + RELEASE_TYPE:'')}"
        contents {
            duplicatesStrategy = 'exclude'
            into('libs-source') {
                from 'core/core-awt/build/libs/'
                from 'viewer/viewer-awt/build/libs/'
                include '*-sources.jar'
            }
            into('libs') {
                // dependency jars
                def libs = []
                libs << project(':core:core-awt').configurations.runtimeClasspath.files
//                libs << project(':viewer:viewer-awt').configurations.runtimeClasspath.files
                from libs
                from project(':core:core-awt').jar
                from project(':viewer:viewer-awt').jar
            }
            // dependencies information
            into('dependencies') {
                from 'build/reports/project'
            }
            // licenses copy
            into('licenses') {
                from 'licenses'
            }
            // maven auto generated pom files.
            into ('maven2') {
                from 'core/core-awt/build/publications/coreJar/pom-default.xml'
                rename { String fileName ->
                    fileName.replace("pom-default.xml", "icepdf-core.xml")
                }
            }
            into ('maven2') {
                from 'viewer/viewer-awt/build/publications/viewerJar/pom-default.xml'
                rename { String fileName ->
                    fileName.replace("pom-default.xml", "icepdf-viewer.xml")
                }
            }
            // examples
//            into ('examples') {
//                from 'examples'
//            }
            // readme.
            into (''){
                from 'readme.html'
                filter(org.apache.tools.ant.filters.ReplaceTokens,
                        tokens: [version: VERSION])
            }
        }
    }
}

dependencies {
    implementation project(':core:core-awt')
    implementation project(':viewer:viewer-awt')
}